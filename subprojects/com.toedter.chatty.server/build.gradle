import com.toedter.chatty.ShutdownServerTask
import com.toedter.chatty.ExecAndWait
import org.apache.tools.ant.taskdefs.condition.Os

apply from: "$rootDir/gradle/integrationTest.gradle"
apply plugin:'application'
apply plugin: 'com.bmuschko.docker-java-application'

version = '1.1'
sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:0.7.2'
    }
}

dependencies {
    compile project(':com.toedter.chatty.model')

    def jerseyVersion = '2.15'
    compile "org.glassfish.jersey.core:jersey-server:$jerseyVersion"
    compile "org.glassfish.jersey.core:jersey-client:$jerseyVersion"
    compile "org.glassfish.jersey.containers:jersey-container-grizzly2-http:$jerseyVersion"
    compile "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    compile "org.glassfish.jersey.media:jersey-media-moxy:$jerseyVersion"

    compile 'com.theoryinpractise:halbuilder-standard:4.0.1'

    compile 'org.eclipse.jetty.aggregate:jetty-all:9.2.7.v20150116'

    compile "org.projectlombok:lombok:1.16.6"
    compile "org.atmosphere:atmosphere-annotations:2.3.4"
    compile "org.atmosphere:wasync:2.1.2"
    compile "org.glassfish.grizzly:grizzly-http-all:2.3.11"

    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.slf4j:jul-to-slf4j:1.7.12'

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.5'
}

mainClassName = 'com.toedter.chatty.server.JettyServer'

docker {
    // local, https and port 2376
    serverUrl = 'https://192.168.59.103:2376'
    certPath = new File('/Users/derk/.boot2docker/certs/boot2docker-vm')

    // local, http and port 2375
    // note: working without TLS must be configured in boot2docker:
    // echo 'DOCKER_TLS=no' >> /var/lib/boot2docker/profile
    // serverUrl = 'http://192.168.59.103:2375'

    javaApplication {
        // either: Java 7 openjdk:
        // baseImage = 'dockerfile/java:openjdk-7-jre'

        // or Java 8:
        baseImage = 'dockerfile/java:oracle-java8'

        maintainer = 'Kai Toedter "kai@toedter.com"'
        tag = 'chatty-server:1.1'
        port = 8080
    }
}

applicationDistribution.from("$projectDir/../com.toedter.chatty.client.web") {
    include "bower_components/**"
    include "src/main/**"
    into('com.toedter.chatty.client.web')
}

dockerCopyDistResources {
    from "$projectDir/../com.toedter.chatty.client.web"
}

task startServerAsyncWithAnt << {
    logger.lifecycle('Starting Jetty asynchronously...')
    def cp = sourceSets.main.runtimeClasspath.asPath
    ant.java(fork: true, spawn: true, classname: "com.toedter.chatty.server.JettyServer", classpath: cp)
}

task startServerSync(type: JavaExec) {
    if (project.hasProperty('port')) {
        args = ['-port=' + port]
    }
    main = 'com.toedter.chatty.server.JettyServer'
    classpath = sourceSets.main.runtimeClasspath
}

task startServerAsync(type: ExecAndWait) {
    project.ext.freePort = findFreePort("$it.name")

    String gradlewExecutable = Os.isFamily(Os.FAMILY_WINDOWS) ? "gradlew.bat" : "./gradlew"
    command "$project.rootDir/$gradlewExecutable :com.toedter.chatty.server:startServerSync -Pport=$project.freePort"
    readyString 'Jetty server started'
    logMessage ":$project.name:$it.name: Jetty server started using port: $project.freePort"
}

task stopServer(type: ShutdownServerTask) {
    port = project.freePort;
}

def int findFreePort(String task) {
    ServerSocket socket = null;
    try {
        socket = new ServerSocket(0);
        int port = socket.getLocalPort();
        // logger.lifecycle(":$project.name:$task: using port {} for integration tests.", port);
        return port;
    } catch (IOException e) {
        logger.lifecycle(":$project.name:$task: cannot find free port, will use 8080.");
        return 8080;
    } finally {
        if (socket != null) {
            try {
                socket.close();
            } catch (IOException e) {
                logger.lifecycle("cannot close socket used for free port creation.");
            }
        }
    }
}
