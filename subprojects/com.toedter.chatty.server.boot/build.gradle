buildscript {
    def springBootVersion = '1.3.0.RELEASE'
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id 'org.hidetake.ssh' version '0.4.3'
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply from: "$rootDir/gradle/integrationTest.gradle"

dependencies {
    compile project(':com.toedter.chatty.model')

    def springBootVersion = '1.3.0.RELEASE'

    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-data-rest:$springBootVersion"
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile "org.springframework.boot:spring-boot-starter-jetty:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    compile "org.springframework.boot:spring-boot-devtools"
    compile "org.springframework.data:spring-data-rest-hal-browser"
    compile "org.projectlombok:lombok:1.16.6"
    compile "org.atmosphere:atmosphere-annotations:2.4.0"
    compile "org.atmosphere:wasync:2.1.2"
    compile "javax.inject:javax.inject:1"
    runtime 'org.hsqldb:hsqldb:2.3.3'

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}

task createBuildInfoFile << {
    def buildInfoFile = new File("$buildDir/resources/main/build-info.properties")
    Properties props = new Properties()
    props.setProperty('version', project.version.toString())
    props.setProperty('timestamp', project.buildTimestamp)
    props.store(buildInfoFile.newWriter(), null)
}

task copyWebApp(type: Copy) {
    from project(':com.toedter.chatty.client.web').file('dist')
    into "$buildDir/resources/main/static"
}

copyWebApp.dependsOn ':com.toedter.chatty.client.web:gruntDist'

jar {
    dependsOn createBuildInfoFile
    // dependsOn copyWebApp
}

task stage(dependsOn: ['jar', 'bootRepackage'])



